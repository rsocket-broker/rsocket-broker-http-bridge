plugins {
	id 'com.jfrog.artifactory' version '4.21.0' apply false
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

apply plugin: 'java-library'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'signing'

sourceCompatibility = '1.8'

repositories {
	if (version.endsWith('SNAPSHOT')) {
		mavenLocal()
		maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
		maven {
			url = uri("https://maven.pkg.github.com/rsocket-broker/rsocket-broker-client")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		}
		maven { url 'https://repo.spring.io/snapshot' }
	}
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

dependencyManagement {
	imports {
		mavenBom "io.projectreactor:reactor-bom:${reactorBomVersion}"
		mavenBom "org.junit:junit-bom:${junitJupiterVersion}"
		mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
		mavenBom "io.rsocket:rsocket-bom:${rsocketVersion}"
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}

	dependencies {
		dependency "io.rsocket.broker:rsocket-broker-client:${rsocketBrokerClientVersion}"
		dependency "io.rsocket.broker:rsocket-broker-common:${rsocketBrokerClientVersion}"
		dependency "io.rsocket.broker:rsocket-broker-common-spring:${rsocketBrokerClientVersion}"
		dependency "io.rsocket.broker:rsocket-broker-frames:${rsocketBrokerClientVersion}"
		dependency "io.rsocket.broker:rsocket-broker-client-spring:${rsocketBrokerClientVersion}"
		dependency "org.assertj:assertj-core:${assertjVersion}"
		dependency "org.mockito:mockito-core:${mockitoVersion}"
		dependency "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
	}
}

dependencies {
	api 'org.springframework.boot:spring-boot-starter-webflux'
	api 'org.springframework.boot:spring-boot-starter-rsocket'
	implementation 'io.rsocket.broker:rsocket-broker-client-spring'
	implementation 'org.springframework.cloud:spring-cloud-function-web'
	implementation 'org.springframework.cloud:spring-cloud-function-rsocket'

	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation'io.projectreactor:reactor-test'
}

plugins.withType(JavaLibraryPlugin) {
	task sourcesJar(type: Jar) {
		classifier 'sources'
		from sourceSets.main.allJava
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		classifier 'javadoc'
		from javadoc.destinationDir
	}

	plugins.withType(MavenPublishPlugin) {
		publishing {
			publications {
				maven(MavenPublication) {
					from components.java
					artifact sourcesJar
					artifact javadocJar
				}
			}
		}
	}
}



test {
	useJUnitPlatform()
}

apply from: "${rootDir}/gradle/publications.gradle"

description = 'RSocket Broker HTTP Bridge'

wrapper {
	distributionType = Wrapper.DistributionType.ALL
}
